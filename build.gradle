plugins {
    id "io.spring.dependency-management" version "${PLUGIN_SPRING_DEPENDENCY_MANAGEMENT_VER}"
    id 'com.github.kt3k.coveralls' version '2.12.0'
    id "com.star-zero.gradle.githook" version "1.2.0"
    id "ru.netris.commitlint" version "1.4.1"
    id "com.diffplug.spotless" version "5.14.2"
    id "com.diffplug.eclipse.apt" version "3.31.0"
}

githook {
    failOnMissingHooksDir = false
    createHooksDirIfNotExist = false
    hooks {
        "commit-msg" {
            task = "commitlint -Dmsgfile=\$1"
        }
        "pre-commit" {
            shell = "sh ./gradle/spotless.sh"
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: "com.diffplug.eclipse.apt"

    group = 'cn.asany.chaos'
    version = System.getenv().getOrDefault('RELEASE_VERSION', 'v0.0.0').replaceAll(/^v/, "")
    status = 'integration'

idea {
        module {
            outputDir file("$buildDir/classes/java/main")
            testOutputDir file("$buildDir/classes/java/test")
            inheritOutputDirs = false
        }
    }

    eclipse {
        jdt {
            apt {
                genSrcDir = file('build/generated/sources/annotationProcessor/java/main')
                genTestSrcDir = file('build/generated_tests')
            }
        }
        classpath {
            file.beforeMerged { cp ->
                cp.entries.add(new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/main/', null))
            }
        }
    }

    repositories {
        maven { url "https://jcenter.bintray.com" }
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply from: "$rootDir/gradle/dependencies.gradle"
    apply from: "$rootDir/gradle/spotless.gradle"

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    idea {
        module {
            outputDir file("$buildDir/classes/java/main")
            testOutputDir file("$buildDir/classes/java/test")
            inheritOutputDirs = false
        }
    }

    eclipse {
        classpath {
            file.beforeMerged { cp ->
                cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder("$buildDir/generated/source/apt/main", null) )
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    compileJava.dependsOn(processResources)

    manifest {
        attributes 'provider': 'gradle'
    }

    javadoc {
        options.tags = ["date"]
        options.addStringOption("charset", "UTF-8")
        if (JavaVersion.current().isJava8Compatible()) {
            options.addBooleanOption('Xdoclint:none', true)
        }
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${LIB_SPRING_BOOT_VER}"
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()

        ignoreFailures = true
    }

    task javadocJar(type: Jar) {
        archiveClassifier = "javadoc"
        from javadoc
    }

    task sourceJar(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allSource
    }

    task copyDependencies(type: Sync) {
        from configurations.compileClasspath
        from configurations.testCompileClasspath
        into 'build/dependencies'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar
                artifact javadocJar

                pom {
                    name = 'jFantasy-Framework'
                    description = 'SpringBoot + JPA + GraphQL'
                    url = 'https://github.com/limaofeng/jfantasy-framework'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://raw.githubusercontent.com/limaofeng/jfantasy-framework/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'limaofeng'
                            name = '李茂峰'
                            email = 'limaofeng@msn.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://git@github.com:limaofeng/jfantasy-framework.git'
                        developerConnection = 'scm:git:ssh://git@github.com:limaofeng/jfantasy-framework.git'
                        url = 'https://github.com/limaofeng/jfantasy-framework'
                    }
                }
            }
        }

        repositories {
            maven {
                if (project.hasProperty('snapshots')) {
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                }
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

}

tasks.register("codeCoverageReport", JacocoReport) {
    subprojects { subproject ->
        subproject.plugins.withType(JacocoPlugin).configureEach {
            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).configureEach { testTask ->
                sourceSets subproject.sourceSets.main
                executionData(testTask)
            }
            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).forEach {
                rootProject.tasks.codeCoverageReport.dependsOn(it)
            }
        }
    }

    reports {
        xml.enabled true
        html.enabled false
        xml.destination file("${buildDir}/reports/Coverage.xml")
    }
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath 'build/reports/Coverage.xml'
}